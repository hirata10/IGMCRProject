set term postscript enhanced eps 24 color
set output "phasespace.eps"
set size 2,1.5
set origin 0,0
set multiplot
set size 1,1.5
set xlabel "log_{10} (p/m_ec)"
set ylabel "(4{/Symbol p} ln 10)p^3f@^0_e(p) [cm^{-3} dex^{-1}]"
set style line 1 lt 1 lw 3.5 dt 1 lc rgb "#000000"
set style line 2 lt 1 lw 1.5 dt 2 lc rgb "#ff0000"
set style line 3 lt 1 lw 1.5 dt 2 lc rgb "#00a000"
set style line 4 lt 1 lw 1.5 dt 2 lc rgb "#0000ff"
unset key
p0 = 9.11e-28*2.998e10
pcrmin = 1.349772990521278e-19
set title "Particle density per log_{10} p"
set grid
set xrange [-4.5:2.5]; set ytics 1
set yrange [-18:-4.5]; set ytics 1
set ytics ("10^{-18}" -18,\
  "10^{-17}" -17, "10^{-16}" -16, "10^{-15}" -15, "10^{-14}" -14, "10^{-13}" -13, "10^{-12}" -12,\
  "10^{-11}" -11, "10^{-10}" -10, "10^{-9}" -9, "10^{-8}" -8, "10^{-7}" -7, "10^{-6}" -6,\
  "10^{-5}" -5)
set label "thermal" at -2.4,-5
set label "photoionization" at -1.7,-12.3 rotate by 45
set label "cosmic rays" at .18,-12.3
plot\
 "phasespace_e_z2.dat" using (log10($1/p0)):(log10($1**3*$4*4.*pi*log(10.))) with lines ls 2,\
 "phasespace_e_z2.dat" using (log10($1/p0)):(log10($1**3*$6*4.*pi*log(10.))) with lines ls 3,\
 "phasespace_e_z2.dat" using (log10($1/p0)*($1>pcrmin?1:1/0)):(log10($1**3*$5*4.*pi*log(10.))) with lines ls 4,\
 "phasespace_e_z2.dat" using (log10($1/p0)):(log10($1**3*$3*4.*pi*log(10.))) with lines ls 1
set origin 1,0
set ylabel "(4{/Symbol p} ln 10)p^3E_e(p)f@^0_e(p) [eV cm^{-3} dex^{-1}]"
set yrange [-12:-4.5]; set ytics 1
set ytics ("10^{-12}" -12,\
  "10^{-11}" -11, "10^{-10}" -10, "10^{-9}" -9, "10^{-8}" -8, "10^{-7}" -7, "10^{-6}" -6,\
  "10^{-5}" -5)
set title "Contribution to energy density per log_{10} p"
unset label
plot\
 "phasespace_e_z2.dat" using (log10($1/p0)):(log10($1**3*$2*$4*4.*pi*log(10.))) with lines ls 2,\
 "phasespace_e_z2.dat" using (log10($1/p0)):(log10($1**3*$2*$6*4.*pi*log(10.))) with lines ls 3,\
 "phasespace_e_z2.dat" using (log10($1/p0)*($1>pcrmin?1:1/0)):(log10($1**3*$2*$5*4.*pi*log(10.))) with lines ls 4,\
 "phasespace_e_z2.dat" using (log10($1/p0)):(log10($1**3*$2*$3*4.*pi*log(10.))) with lines ls 1
unset multiplot
